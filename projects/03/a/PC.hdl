// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Read a value into the register if any of load, inc, or reset are set
    Or(a=load, b=inc, out=doload);
    Or(a=reset, b=doload, out=read);

    // If a value should be read, it'll be in, count, or 0
    Mux16(a=count, b=in, sel=load, out=value);
    Mux16(a=value, b=false, sel=reset, out=set);

    Register(in=set, load=read, out=regout, out=out);
    Inc16(in=regout, out=count);
}
